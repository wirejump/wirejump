# MikroTik router setup

This guide will guide through router setup with a focus on CLI usage. Of course, setup can be performed via WinBox/Web UI as well.

NOTE: actual Internet connectivity is configured in the next (and final) step: [VPN configuration](./upstream.md). It's completely okay if you have no Internet after this section is completed!

# Before you begin

- BACKUP YOUR CURRENT SETTINGS: https://help.mikrotik.com/docs/display/ROS/Backup
- Ensure your device is running RouterOS 7. Version 6 is not supported!


If you make a mistake in firewall rules, you can easily lock yourself out. Before this happens, take one of these precautions:
- Always use Safe Mode (WinBox has a button for that): https://wiki.mikrotik.com/wiki/Manual:Console#Safe_Mode
- Check if you can connect to your router by MAC address via WinBox: https://wiki.mikrotik.com/wiki/MAC_access;
- If your device has dedicated console port, ensure it's working;
- If your device has USB port, you can buy this thing https://mikrotik.com/product/woobm
- Instead of Woobm-USB stick you can use 2 USB to serial adapters. Cross-connect data (TX->RX, RX->TX) and ground lines. Don't connect power lines! Put one of the adapters into your device and another one into your machine. You should be able to connect with these settings: 115200 8N1

Overall process is very simple, but firewall setup can be challenging at first, especially if you have a lot of custom rules already. There are three main steps of the configuration:

- Creating the interface
- Connecting to server
- Firewall setup

# Creating the interface

Let's create an interface called `wirejump1`:

```
/interface/wireguard add name=wirejump1 comment="WireJump: main interface"
```

This will create private-public keypair automatically:
```
/interface/wireguard print where name=wirejump1 
Flags: X - disabled; R - running 
 1  R name="wirejump1" mtu=1420 listen-port=35498 
      private-key="SLdBWOW0kCT+mQRNSZNoErM7T/k+w5CVOlBP7cpExmQ=" 
      public-key="EoEQnsnUqfjN2GMvSa1TA0/SAzj6bIV/R6Cx4xuHElQ=" 
```

If you're not satisfied with autogenerated keys, you can either recreate the interface, or just replace private key with some key generated elsewhere. There's no need to replace public key as it will be derived from private one:
```
/interface/wireguard set private-key="wPYATBX8gRn8tz+rn6HDhGan1eg+4HkEw6MGAt2W7Gk=" [find where name=wirejump1]
/interface/wireguard print where name=wirejump1
Flags: X - disabled; R - running 
 1  R name="wirejump1" mtu=1420 listen-port=35498 private-key="wPYATBX8gRn8tz+rn6HDhGan1eg+4HkEw6MGAt2W7Gk=" 
      public-key="B8uaRHLiYZDmfErZjAdtVpzPKa0szFnGGL2U7Wl+rR0="
```

# Connecting to server

Once you've got your keys ready, you need to query server for the IP address. Login to the server and add a new peer:

```
$ wjcli peer --add --pubkey B8uaRHLiYZDmfErZjAdtVpzPKa0szFnGGL2U7Wl+rR0=
Peer
  IPv4 Address:         172.16.1.128/24
  Isolated:             no
```

This command will return the IP address which server has allocated for a particular public key. By default, all peers are NOT isolated and can communicate with each other. If such configuration is not desirable, pass `--isolated` flag.

> Don't worry about `/24` netmask. It's mainly needed in order to reach the server (`172.16.1.1` by default), but will allow you to access other peers (and them to access you). Since all communication between peers over `172.16.1.0` network will pass through the server anyway, in case you've got an `isolated` address, server just won't forward any packets to or from you. Of course, you can specify something like `/31` here.


Let's add this address to the interface:

```
/ip/address add address=172.1.16.128/24 network=172.16.1.0 interface=wirejump1 comment="WireJump: local peer"
```

Now it's time to configure interface peers. You'll have a single peer, which is your server:

```
/interface wireguard peers add allowed-address=0.0.0.0/0 endpoint-address=1.2.3.4 endpoint-port=51820 interface=wirejump1 public-key="abc" comment="WireJump: server"
```

In this example:
- `1.2.3.4` is your server public address;
- `51820` is the default WireGuard port;
- `abc` is the public key of your server. Don't forget the quotes as it may contain `=` symbol; 
- `0.0.0.0/0` specifies which IP addresses are allowed to be sent/received through this peer. To use `wirejump1` as your new Internet interface, you need to specify `0.0.0.0/0` here (all possible addresses). This guide focuses on allowing all here and then restricting access via firewall. If you have other plans in mind, setup this field accordingly.

If your ISP is DSL/cable/satellite, chances are your router is behind ISP NAT and your public IP is changing from time to time. In this case you'll need to specify a [keepalive setting](https://www.wireguard.com/quickstart/#nat-and-firewall-traversal-persistence):

```
/interface/wireguard/peers set persistent-keepalive=25 [find where interface=wirejump1]
```


At this point, tunnel should _just workâ„¢_. Verify connectivity by pinging your server:

```
/ping 172.16.1.1      
  SEQ HOST                                     SIZE TTL TIME       STATUS                                                                         
    0 172.16.1.1                                 56  64 16ms163us 
    1 172.16.1.1                                 56  64 15ms854us 
    2 172.16.1.1                                 56  64 16ms196us 
    sent=3 received=3 packet-loss=0% min-rtt=15ms854us avg-rtt=16ms71us max-rtt=16ms196us 

```

If ping doesn't go through:
- Enable/disable (or restart) the interface and/or peer;
- Verify that your client `PublicKey` is present at the `[Peer]` section on the server (file `/opt/config/wirejump/downstream.conf`);
- Ensure that server's `PublicKey` (`[Interface]` section of the same file) is mathing your client's peer configuration;
- Check if you're using the right address for your interface. If you've specified address/netmask other than the server gave you, packets won't go through (field is called _Allowed IPs_ for a reason);
- Ensure your local or ISP firewall are not blocking your server's public address and WireGuard port;
- Check out **Tunnel link establishment** section of  [troubleshooting guide](./troubleshooting.md)


Last, but not least: to make server to forward data you put in the tunnel to your VPN, masquerading rule needs to be added: 

```
/ip/firewall/nat add action=masquerade chain=srcnat out-interface=wirejump1 comment="WireJump: masquerade"
```

At this point you have a connection to your WireJump server, which can route stuff to the Internet when VPN connection is active. If it's not active, your packets will be rejected. If peer is not `isolated` (default), you would be able to access other peers via `172.16.1.0/24` network even if VPN is down.

# Firewall setup

Once the tunnel is established and VPN connection is active, tunnel will forward everything to the VPN server of your choice (and vice versa). Treat this tunnel as your primary Internet connection: restrict firewall access, setup conditional routing, etc.

This section will give examples of two setups:
- Simple - unconditionally forward everything into the tunnel;
- Advanced - explicitly specify what should be forwarded.

There are pros and cons for each approach.

## Simple setup

Pros:
- Hard killswitch;
- Simple configuration;
- Everything, including router itself, goes to the Internet via the tunnel;
- More performant if you have a lot of users;

Cons:
- You're messing with your default Internet connection
- If your ISP default gateway changes, you have no Internet at all
- If you redeploy your server or it somehow goes down, you have no Internet at all

In this setup, you remove your default route advertised by your ISP and replace it with a route to your WireJump server. 
Whole process it the following:
1. Identify your primary Internet connection and your default gateway. In this example, Internet is provided by ISP modem, which is connected to the dedicated router. Default gateway is the address of a modem (`192.168.100.1`) on the network it creates (`192.168.100.0/24`) and to which the router is connected.
2. Add a static route to your WireJump server (`1.2.3.4`). Sometimes interface name (`ether1` in this case) is needed:
```
/ip/routes add dst-address=1.2.3.4 gateway=192.168.100.1
```
3. Remove (or disable) route advertised by the modem (sometimes interface name is needed):
```
/ip/routes remove dst-address=0.0.0.0/0 gateway=192.168.100.1
```

> At this point, you should still be able to ping your server via its internal (`172.16.1.1`) AND external (`1.2.3.4`) addresses. If that's not the case, ensure that the static route you have created before uses the right gateway.

4. Add new default route:
```
/ip/routes add dst-address=0.0.0.0/0 gateway=wirejump1
```
5. Remove (or disable) default masquerading rule which features the interface modem is connected to (`ether1`):
```
/ip/firewall/nat remove [find where action="masquerade" and out-interface="ether1"]
```

## Advanced setup

Pros:
- Much more flexible
- Completely independent of your ISP connection

Cons:
- Harder to setup
- No default killswitch: it only applies to the devices/networks you specify;

---

**DISCLAIMER:**

- If you're ~~paranoid~~ worried about unwanted access from your WireJump server, use `--isolated` setting when adding a peer and add a dedicated firewall rule to block incoming connections from `172.16.1.0/24` network to the INPUT chain (`/ip/firewall/filter`).
- Comparing to the previous method which unconditionally routes everything via the tunnel, here you explicitly select what should go to the VPN. It's **UP TO YOU** to ensure your settings are correct. In other words: if you change your network configuration and forget to update the settings, your network will not use the VPN!
- This method features two options: mangle and routing rules. Despite it's technically possible to use them together, it's NOT RECOMMENDED unless you absolutely know what you're doing. More info: https://help.mikrotik.com/docs/display/ROS/Policy+Routing

---

VPN providers often call this configuration "split tunelling" in their apps, because only specific traffic is going into the tunnel. This setup involves creation of a separate routing table and then defining which traffic can use it.

This can be achieved by two ways: either by using routing rules or by firewall mangle rules. 


In both cases, a dedicated routing table will be required. So let's create it and add a default route for it:

```
/routing/table add fib name=wirejump
/ip/route add dst-address=0.0.0.0/0 gateway=wirejump1 routing-table=wirejump comment="WireJump: default route"
```



### Option A: routing rules

This option may be sufficient if you know your network configuration beforehand: let's say, you want host A and network B to use VPN, and everything else should use your default Internet connection.

For example, to make host `192.168.24.29` use the tunnel, add following rule:

```
/routing/rule add table=wirejump src-address=192.168.24.29/32 action=lookup-only-in-table comment="WireJump: explicit rule for 192.168.24.29"
```

`lookup-only-in-table` means that specified rule entry should not use main routing table in case it can't find destination in the `wirejump` one. Essentially, it's a killswitch: it ensures that if `wirejump1` interface is not routable (e.g. it's down or server is not accessible), traffic from `192.168.24.29` will not leave your router.

As you can see, routing rules are quite limited and only allow you to specify SOURCE and DESTINATION addresses/networks, but nothing else (no domains, protocol types, ports etc).

### Option B: firewall mangle

Beauty of mangle method is that you can filter anything with firewall rules: source, destination, protocol type, port number etc. This example will focus on addresses and hostnames for simplicity.

Let's create two _Address Lists_. One of them will define **SOURCE** addresses, which are going to be forwarded into the tunnel. You can specify your whole LAN (`192.168.10.0/24` in this example) here:

```
/ip/firewall/address-list add address=192.168.10.0/24 list="WireJump" comment="WireJump: source"
```

Second list will contain **DESTINATION** addresses and will serve as exception list. This can be especially useful if you want to access some streaming services which alter or disable content for VPN users. Let's use Ubuntu website as the example:

```
/ip/firewall/address-list add address=ubuntu.com list="WireJumpExclude" comment="WireJump: exclude destination"
```

The main benefit of using address lists over routing rules is that you can specify domain names. If you do, RouterOS will resolve them and add dynamic address list entries.

After you've filled the lists, add few necessary firewall rules ON TOP of your MANGLE list (in this example, `ether1` is your WAN interface):

```
/ip/firewall/mangle add action=jump chain=prerouting comment="WireJump: jump to marking" connection-state=new jump-target=mark-connections
/ip/firewall/mangle add action=accept chain=prerouting comment="WireJump: accept return connections" in-interface=wirejump1
/ip/firewall/mangle add action=accept chain=prerouting comment="def: accept return connections" in-interface=ether1
add action=mark-routing chain=prerouting comment="WireJump: mark routing" connection-mark=wirejump new-routing-mark=wirejump passthrough=no
add action=mark-connection chain=mark-connections comment="WireJump: mark desired traffic" connection-mark=no-mark dst-address-list=!WireJumpExclude dst-address-type=!local new-connection-mark=wirejump passthrough=yes src-address-list=WireJump
```

These rules define a new chain (`mark-connections`) which will be used for all _new_ connections coming to your router.

If these connections
- are coming from hosts/networks in `WireJump` list
- don't have their destination in `WireJumpExclude` list
- don't have any connection mark
- are not targeting any of your router interfaces

â€“ they are going to be marked. Everything with this connection mark gets routing mark applied, which means it's going to be routed via another (not `main`) routing table. `accept` rules ensure that traffic which's coming _back_ to your router (either from the tunnel or from default WAN interface) is not going to be dropped or misrouted.

Why separate rules? It's certainly possible to apply routing mark right from the beginning, but it's not very efficient, since it's going to be applied to each _packet_. Adding `connection-mark` first allows to group all packets belonging to a certain connection and is much more effective. It also enables you to use FastTrack if you wish to do so:

```
/ip/firewall/filter add action=fasttrack-connection chain=forward comment="FastTrack established, related and unmarked" connection-mark=no-mark connection-state=established,related hw-offload=yes
```

---

**IMPORTANT:**

For mangle rules to work, you need to have [rp-filter](https://help.mikrotik.com/docs/display/ROS/IP+Settings) setting to be either `loose` or `no`. If you require `strict` functionality, it can be achieved by RAW firewall chain.

---

Finally, let's add a killswitch, which will ensure that your target network is not leaving the router if WireJump server is not accessible:

```
/ip/firewall/filter add chain=forward action=reject reject-with=icmp-host-unreachable out-interface=!wirejump1 routing-mark=wirejump comment="WireJump: killswitch"
/ip/firewall/filter move [find comment="WireJump: killswitch"] destination=1
```

# Congrats!

You have completed the most hard part of the configuration. Now it's time to SSH into your server and proceed with the [VPN configuration](./upstream.md).
